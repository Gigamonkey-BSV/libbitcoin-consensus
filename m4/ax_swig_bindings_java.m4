
AC_DEFUN([AX_SWIG_BINDINGS_JAVA], [
    JAVA_BINDING_PACKAGE_NAME=
    JAVA_BINDING_PACKAGE_PATH=

    if test -n "$1" ; then
        JAVA_BINDING_PACKAGE_NAME="$1"
        JAVA_BINDING_PACKAGE_PATH=`echo "$1" | sed 's/\./\//g'`
    else
        AC_MSG_ERROR(["Required java package name not provided."])
    fi

    AC_SUBST([JAVA_BINDING_PACKAGE_NAME])
    AC_SUBST([JAVA_BINDING_PACKAGE_PATH])

    AC_PROG_MKDIR_P
    AX_PROG_JAVAC
    AX_PROG_JAVA
    AX_PROG_JAR
    AX_JNI_INCLUDE_DIR

    JAVA_BINDING_CFLAGS=

    for JNI_INCLUDE_DIR in $JNI_INCLUDE_DIRS; do
      JAVA_BINDING_CFLAGS="$JAVA_BINDING_CFLAGS -I$JNI_INCLUDE_DIR"
    done

    AC_SUBST([JAVA_BINDING_CFLAGS])

    JAVA_BINDING_LIBS=
    AC_SUBST([JAVA_BINDING_LIBS])

    JAVA_BINDING_PATH_BASE=bindings/java
    AC_SUBST([JAVA_BINDING_PATH_BASE])

    JAVA_BINDING_PATH_CPP=${JAVA_BINDING_PATH_BASE}/cpp
    AC_SUBST([JAVA_BINDING_PATH_CPP])

    JAVA_BINDING_PATH_LANG=${JAVA_BINDING_PATH_BASE}/lang
    AC_SUBST([JAVA_BINDING_PATH_LANG])
])
